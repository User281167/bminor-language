/* =================================================================== *
 *                                                                     *
 * knight.bminor                                                       *
 *                                                                     *
 * El problema del caballo es un antiguo problema matemático en el que *
 * se pide que, teniendo una cuadrícula de n x n casillas y un caballo *
 * de ajedrez colocado en una posición cualquiera ( x, y ), el caballo *
 * pase por todas las casillas y una sola vez.                         *
 *                                                                     *
 * =================================================================== */

N: constant = 5;       // Tamaño del tablero
recu: auto = 0;

index: function integer (r: integer, c:integer) = {
    return r * N + c;
}

// Función auxiliar para imprimir el tablero
print_board: function void (board: array [] integer) = {
    r: integer;
    c: integer;

    for (r = 0; r < N; r++) {
        for (c = 0; c < N; c++) {
            print board[index(r,c)], " ";
        }

        print "\n";
    }
}

// Función auxiliar para validar movtos válidos
can_move: function boolean (r: integer, c: integer, board: array [] integer) = {
    return r >= 0 && r < N &&
           c >= 0 && c < N &&
           board[index(r,c)] == 0;
}

// Función recursiva para que el caballo recorra el tablero
walk: function boolean (r: integer, c: integer, m: integer, board: array [] integer, xmoves: array [] integer, ymoves: array [] integer) = {
    i: integer;

    if (m == N * N + 1) {
        print_board(board);
        return true;
    }

    recu++;

    for (i = 0; i < 8; i++) {
        next_x: integer = r + xmoves[i];
        next_y: integer = c + ymoves[i];

        if (can_move(next_x, next_y, board)) {
            board[index(next_x, next_y)] = m;

            if (walk(next_x, next_y, m+1, board, xmoves, ymoves)) {
                return true;
            }

            board[index(next_x, next_y)] = 0;
        }
    }
    return false;
}

main: function void () = {
    board: array [N*N] integer;
    i: integer;

    for (i = 0; i < N*N; i++)
        board[i] = 0;

    xmoves: array [8] integer = {2, 1, -1, -2, -2, -1, 1, 2};
    ymoves: array [8] integer = {1, 2, 2, 1, -1, -2, -2, -1};

    board[0] = 1;   // marcar casilla inicial

    if (walk(0, 0, 2, board, xmoves, ymoves)) {
        print "Solucion encontrada!\n";
    } else {
        print "No existe solucion\n";
    }

    print recu, '\n';
}

main();